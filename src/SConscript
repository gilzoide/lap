import SCons
import os

# prepare the Install function with possible permission override
# copied from: https://bitbucket.org/scons/scons/wiki/InstallTargets
from SCons.Script.SConscript import SConsEnvironment
SConsEnvironment.Chmod = SCons.Action.ActionFactory(os.chmod,
        lambda dest, mode: 'Chmod("%s", 0%o)' % (dest, mode))

def InstallPerm(env, dest, files, perm = 0644):
    obj = env.Install(dest, files)
    for i in obj:
        env.AddPostAction(i, env.Chmod(str(i), perm))
    return dest

# put this function "in" scons
SConsEnvironment.InstallPerm = InstallPerm

# lap build script
Import ('env')

src = Glob ('*.cpp')
liblap = env.SharedLibrary ('liblap.so', src)

Default (liblap)

## INSTALL STUFF ##
# includes -> *.h
includes = Glob ('#include/*.h')
# pkg-config files -> *.pc
pkgconfigs = Glob ('#pkgconfigs/*.pc')

env.InstallPerm ('$PREFIX/usr/lib', liblap)
env.InstallPerm ('$PREFIX/usr/include/mosaic', includes)
env.InstallPerm ('$PREFIX/usr/share/pkgconfig', pkgconfigs)
# targets: install{,-bin,-lib,-include}
env.Alias ('install-lib', '$PREFIX/usr/lib')
env.Alias ('install-include', '$PREFIX/usr/include')
env.Alias ('install-pkgconfig', '$PREFIX/usr/share/pkgconfig')
env.Alias ('install', ['install-lib', 'install-include', 'install-pkgconfig'])
